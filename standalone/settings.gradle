buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
        // These repositories contain the LabKey gradle plugin
        maven {
            url "${artifactory_contextUrl}/plugins-release-no-proxy"
            mavenContent {
                releasesOnly()
            }
        }
        maven {
            url "${artifactory_contextUrl}/plugins-snapshot-local"
            mavenContent {
                snapshotsOnly()
            }
        }
    }
    dependencies {
        classpath "org.labkey.build:gradlePlugins:${gradlePluginsVersion}"
    }
    configurations.configureEach {
        // Check for updates every build for SNAPSHOT dependencies
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}

// This ensures that the module name is correct when cloned to a non-standard directory
rootProject.name="standalone"

if (!hasProperty("labkeyVersion")) {
    throw new GradleException("Define 'labkeyVersion' in 'gradle.properties' to build outside of a standard LabKey enlistment")
}

gradle.beforeProject { project ->
    project.repositories {
        // Maven Central first
        mavenCentral()
        maven {
            // Use this repository when relying on release versions of the LabKey artifacts and their external dependencies
            url "${project.artifactory_contextUrl}/libs-release-no-proxy"
            mavenContent {
                releasesOnly()
            }
        }
        maven {
            // Use this repository when relying on snapshot versions of LabKey artifacts
            url "${project.artifactory_contextUrl}/libs-snapshot-no-proxy"
            mavenContent {
                snapshotsOnly()
            }
            content {
                includeGroup "org.labkey"
                includeGroup "org.labkey.api"
                includeGroup "org.labkey.module"
            }
        }
    }
}
